// https://github.com/raydac/java-comment-preprocessor/wiki/AndroidGradlePreprocessing

// the folder contains the result
def jcpRoot = "${buildDir}/jcp"
// the original root folder path
def srcRoot = 'upstream/pgjdbc/src'

configurations {
    jcp
    processedMainCompile
}

dependencies {
    compile 'com.github.dblock.waffle:waffle-jna:1.7.5'
    compile 'net.java.dev.jna:jna:4.2.1'
    compile 'net.java.dev.jna:jna-platform:4.2.1'
    compile 'org.slf4j:jcl-over-slf4j:1.7.12'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.3'
    compile 'org.osgi:org.osgi.enterprise:4.2.0'
    compile 'org.osgi:org.osgi.core:4.3.1'
    compile 'com.ongres.scram:client:1.0.0-beta.2'
    processedMainCompile configurations.compile
    jcp('com.igormaznitsa:jcp:6.0.1') {
        exclude group: 'org.apache.maven.*'
        exclude group: 'org.codehaus.*'
    }
}

if (!hasProperty("buildProfile")) ext.buildProfile = "jre8"

task preprocessJava {
    doLast {
        def jcpjar = file(project.configurations.jcp.find { it.name.startsWith("jcp-") })
        ant.taskdef(resource: 'com/igormaznitsa/jcp/ant/antlib.xml', classpath: jcpjar)

        ['/main/java', '/main/resources'].each { dir ->
            def fromDir = srcRoot + dir;
            def toDir = jcpRoot + dir;
            println "Preproces: ${fromDir} -> ${toDir}"
            ant.preprocess(excluded: "none", processing: 'java,xml', source: fromDir, destination: toDir) {
                cfgFile(file: "${buildProfile}.properties")
            }
        }
    }
}

task preprocessTestJava {
    doLast {
        def jcpjar = file(project.configurations.jcp.find { it.name.startsWith("jcp-") })
        ant.taskdef(resource: 'com/igormaznitsa/jcp/ant/antlib.xml', classpath: jcpjar)

        ['/test/java', '/test/resources'].each { dir ->
            def fromDir = srcRoot + dir;
            def toDir = jcpRoot + dir;
            println "Preproces: ${fromDir} -> ${toDir}"
            ant.preprocess(excluded: "none", processing: 'java,xml', source: fromDir, destination: toDir) {
                cfgFile(file: "${buildProfile}.properties")
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir "${srcRoot}/main/java"
            exclude '**/osgi/*'
        }
        resources {
            srcDir "${srcRoot}/main/resources"
            exclude '**/services/java.sql.Driver'
        }
    }
    processedMain {
        java {
            srcDir "${buildDir}/jcp/main/java"
            exclude '**/osgi/*'
        }
        resources {
            srcDir "${buildDir}/jcp/main/resources"
            exclude '**/services/java.sql.Driver'
        }
    }
}

compileJava {
    dependsOn preprocessJava
    source = sourceSets.processedMain.java
    options.warnings = false
    options.deprecation = false
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}

test {
    enabled = false;
}
